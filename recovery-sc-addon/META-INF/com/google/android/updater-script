# PA GApps Keyboard Module Installation Script - Updated: 2014-10-18

ui_print(" ");
ui_print("#########################################");
ui_print(" ___   _      ___    _                   ");
ui_print("| _ \ /_\    / __|  /_\   _ __  _ __  ___");
ui_print("|  _// _ \  | (_ | / _ \ | '_ \| '_ \(_-<");
ui_print("|_| /_/ \_\  \___|/_/ \_\| .__/| .__//__/");
ui_print("                         |_|   |_|       ");
ui_print("#########################################");
ui_print(" ");
ui_print("PA GApps Keyboard-Module 4.4.4 - 20141019");
ui_print(" ");
sleep(3);
ui_print("Mounting system...");
run_program("/sbin/busybox", "mount", "/system");
ui_print(" ");

# Checking to make certain user has a 4.4.x ROM Installed - If not, we abort
ifelse(
    is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")),
    (
        ui_print("Android 4.4.x ROM detected");
        ui_print(" ");
    ),
    (
        ui_print("*** Incompatible Android ROM detected ***");
        ui_print(" ");
        ui_print("This Addon pkg is for Android 4.4.x ONLY");
        ui_print(" ");
        ui_print("******* Addon Installation failed *******");
        ui_print(" ");
        ui_print("Unmounting system...");
        run_program("/sbin/busybox", "umount", "/system");
        ui_print(" ");
        sleep(7);
        abort("Aborting...");
    )
);

show_progress(0.7, 6);

# Extract required package files to /tmp and run the preparation script
package_extract_file("install-prep.sh", "/tmp/install-prep.sh");
set_perm(0, 0, 0777, "/tmp/install-prep.sh");
run_program("/tmp/install-prep.sh", file_getprop("/system/build.prop","ro.product.device"));

# Check that sufficient space exists in the system partition to install this Addon
# Calculations should be extremely accurate and include a 10MB buffer to allow for ROM growth
ifelse(
    less_than_int(file_getprop("/tmp/addon.prop","free_system_size_kb"), file_getprop("/tmp/addon.prop","free_space_reqd_kb")),
    (
        ui_print("Insufficient storage space available in");
        ui_print("System partition. You may want to install");
        ui_print("Google Keyboard from Play Store instead.");
        ui_print(" ");
        ui_print("You need ",file_getprop("/tmp/addon.prop","free_space_reqd_kb")," KB system free space.");
        ui_print("You only have ", file_getprop("/tmp/addon.prop","free_system_size_kb")," KB available.");
        ui_print(" ");
        ui_print("If you have further questions, please");
        ui_print("post the pa_addon.log file that has been");
        ui_print("copied to /sdcard/.");
        ui_print(" ");
        ui_print("****** Addon Installation failed ******");
        ui_print(" ");
        ui_print("Unmounting system...");
        run_program("/sbin/busybox", "umount", "/system");
        ui_print(" ");
        sleep(15);
        abort("Aborting...");
    )
);

# Removing /sdcard/pa_addon.log from the SDCard as there were no problems detected
delete("/sdcard/pa_addon.log");

# Removing the stock/AOSP Keyboard and library (since we're adding Google Keyboard)
delete("/system/app/LatinIME.apk","/system/lib/libjni_latinimegoogle.so","/system/app/LatinImeGoogle.odex");

# Removing a 'Rogue' Keyboard app found in Velocity (and possibly other) ROM's (since we're adding Google Keyboard)
delete("/system/app/GoogleLatinIme.apk");

# Main install process - copying over system files

show_progress(1, 2);
ui_print("Installing Addon...");
ui_print(" ");
package_extract_dir("system", "/system");

show_progress(0.3, 2);
ui_print("Fixing Permissions...");
ui_print(" ");
set_perm(0, 0, 0755, "/system/addon.d/72-keyboards.sh");
set_perm_recursive(0, 0, 0755, 0644, "/system/app");

ui_print("Unmounting system...");
ui_print(" ");
run_program("/sbin/busybox", "umount", "/system");
ui_print("Addon Installation Complete!");
ui_print(" ");
sleep(3);
